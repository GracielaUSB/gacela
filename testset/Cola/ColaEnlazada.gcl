#!/usr/bin/env rungraciela
/*% LANGUAGE EnableTrace %*/
/*% LANGUAGE LogicAnywhere %*/

program Cola begin

abstract ColaA(T)
begin
  const MAX := 100 : int;
  var contenido : sequence of T;

  {repinv #contenido <= MAX repinv}

  proc encolar (inout c : ColaA(T), in x : T)
    let contenido_0 := c.contenido : sequence of T;
    {pre #c.contenido < c.MAX pre}
    {post c.contenido == contenido_0 ++ ⟨x⟩ post}

  proc desencolar (inout c : ColaA(T) )
    let tail := ⟨i : int | 1 ≤ i /\ i < #c.contenido | c.contenido[i]⟩ : sequence of T;
    {pre c.contenido != ⟨⟩ pre}
    {post c.contenido == tail post}

  proc primero (in c : ColaA(T), out x : T)
    {pre c.contenido != ⟨⟩ pre}
    {post x == c.contenido[0] post}

  proc vacia (in c : ColaA(T), out v : boolean )
    {pre true pre}
    {post v ≡ (c.contenido == ⟨⟩) post}
end

abstract ASubCola (T)
begin
  var subcont : sequence of T;
  {repinv #subcont > 0 repinv}
end

type SubCola (T) implements ASubCola(T)
begin
  var item : T;
  var indice : int;
  var sig : SubCola (T) *;

  {repinv true repinv}
  {coupinv true coupinv}
  where { subcont := ⟨item⟩ ++ if sig == null -> ⟨⟩
                              [] sig != null -> (*sig).subcont
                              fi }

  proc mostrar' (in c : SubCola (T), in B : int)
    {pre true pre}
    {post true post}
    {bound B - c.indice bound}
    |[ write (c.item)
     ; if c.sig == null -> skip
       [] c.sig != null -> write (" ")
                          ; mostrar' (*c.sig, B)
       fi
    ]|
end

type Cola (T) implements ColaA(T)
begin
  var sub : SubCola (T) *;
  var ult : SubCola (T) *;
  var elems : int;

  {repinv ult == null \/ (*ult).sig == null repinv}
  {coupinv true coupinv}

  where { contenido := if sub == null -> ⟨⟩
                       [] sub != null -> (*sub).subcont
                       fi }

  proc encolar (inout c : Cola (T), in x : T)
    {pre c.elems < c.MAX pre}
    {post true post}
    |[ if c.sub == null
         -> new (c.sub)
          ; (*c.sub).item := x
          ; (*c.sub).indice := 0
          ; (*c.sub).sig := null
          ; c.ult := c.sub
       [] c.sub != null
         -> var ult' : SubCola (T) *
          ; new (ult')
          ; (*ult').item := x
          ; (*ult').indice := (*c.ult).indice + 1
          ; (*ult').sig := null
          ; (*c.ult).sig := ult'
          ; c.ult := ult'
       fi
     ; c.elems := c.elems + 1
    ]|

  proc desencolar (inout c : Cola (T) )
    {pre c.sub != null pre}
    {post true post}
    |[ if c.sub == null -> abort
       [] c.sub != null 
         -> var aux : SubCola(T) *
          ; aux := (*c.sub).sig
          ; free (c.sub)
          ; c.sub := aux
          ; c.elems := c.elems - 1
          ; if c.elems == 0 -> c.ult := null
            [] otherwise -> skip
            fi
       fi
    ]|

  proc primero (in c : Cola (T), out x : T)
    {pre c.sub != null pre}
    {post x == (*c.sub).item post}
    |[ x := (*c.sub).item ]|

  proc vacia (in c : Cola (T), out v : boolean )
    {pre true pre}
    {post v ≡ c.sub == null post}
    |[ v := c.sub == null ]|

  proc mostrar (in c : Cola (T))
    {pre true pre}
    {post true post}
    |[ if c.sub == null
         -> skip
       [] otherwise
         -> mostrar' (*c.sub, c.MAX + (*c.sub).indice)
       fi
    ]|

  proc ejecutar (in c : Cola (T))
    {pre true pre}
    {post true post}
    |[ var veces : int
     ; var vez   : int
     ; var opcion : char

     ; var aux : T
     ; var v : boolean

     ; /*write ("Cuantas veces desea interactuar con el sistema? > "); */read (veces)

     ; vez := 1
     ; {inv true inv}
       {bound veces - vez + 1 bound}
       do vez <= veces 
         -> /*write (vez); write ("> ");*/ read (opcion)
          ; if opcion == 'E' \/ opcion == 'e'
              -> /*write ("escriba el entero a encolar > ");*/ read (aux)
               ; if c.elems < c.MAX -> encolar (c, aux)
                 [] otherwise       -> writeln ("No.")
                 fi
            [] opcion == 'D' \/ opcion == 'd'
              -> vacia (c, v)
               ; if  v -> writeln ("No.")
                 [] !v -> desencolar (c)
                 fi
            [] opcion == 'V' \/ opcion == 'v'
              -> vacia (c, v)
               ; writeln (v)
               // ; if v         -> writeln ("la cola esta vacia.")
               //   [] otherwise -> writeln ("la cola no esta vacia.")
               //   fi
            [] opcion == 'C' \/ opcion == 'c'
              -> vacia (c, v)
               ; if  v -> writeln ("No.")/*writeln ("la cola esta vacia.")*/
                 [] !v -> primero (c, aux)
                               ; writeln (/*"la cabeza de la cola es ", */aux)
                 fi
            [] opcion == 'P' \/ opcion == 'p'
              -> mostrar (c); writeln ()
            [] opcion == 'S' \/ opcion == 's'
              -> vez := veces
            [] otherwise 
              -> skip // writeln ("**Ayuda para colas**")
               // ; writeln ("Comandos:")
               // ; writeln ("\t(E)ncolar")
               // ; writeln ("\t(D)esencolar")
               // ; writeln ("\t(V)acia")
               // ; writeln ("\t(C)abeza")
               // ; writeln ("\tim(P)rimir")
               // ; writeln ("\t(S)alir")
               // ; writeln ("\t(*)ayuda (este mensaje)")
            fi
          ; vez := vez + 1
       od
     // ; writeln ("Ha llegado al final del programa.")
     // ; write ("Último valor de la cola: ")
     ; mostrar (c)
     ; writeln ()
    ]|
end

main
  |[ var t : char
   ; read (t)
   ; if t == 'b' 
       -> var cb : Cola (boolean)
        ; ejecutar (cb)
     [] t == 'c' 
       -> var cc : Cola (char)
        ; ejecutar (cc)
     [] t == 'f' 
       -> var cf : Cola (float)
        ; ejecutar (cf)
     [] t == 'i' 
       -> var ci : Cola (int)
        ; ejecutar (ci)
     [] otherwise 
       -> skip
     fi
  ]|
end
