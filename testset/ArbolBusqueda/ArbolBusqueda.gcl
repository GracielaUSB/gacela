#!/usr/bin/env rungraciela

/*% LANGUAGE EnableTrace %*/

program SearchTree.bin begin
  abstract n
  begin
    var i : n*;
    {repinv true repinv}
    proc f ( in x : n )
      {pre  true pre}
      {post true post}
  end

  type m implements n
  begin
    {repinv true repinv}
    {coupinv true coupinv}
    proc f ( in x : m )
      {pre  true pre}
      {post true post}
      |[ skip ]| 
  end


  abstract Multiset(T)
  begin
    /* Multiset variables */
    var m   : multiset of T;
    
    {repinv 
        (% forall i : T | i elem m | i == i %) 
    repinv}

    /* Multiset procedures and functions */   
    proc insert ( inout t : Multiset(T)*, in e : T )

      let m' := if t != null -> (*t).m
                   [] otherwise -> {::}     
                   fi 
              : multiset of T;
      {pre  true  pre}
      {post e elem m' ==> 
              multiplicity(e,m') == multiplicity(e,(*t).m) + 1
      post}
    
    proc delete ( inout t : Multiset(T)*, in e : T )
      let m' := if t != null -> (*t).m
                   [] otherwise -> {::}     
                   fi
              : multiset of T;
      {pre  multiplicity(e,(*t).m) > 0 pre}
      {post multiplicity(e,m') == multiplicity(e,(*t).m) - 1 post}

    func isElem ( t : Multiset(T)*, e : T ) -> boolean
      {pre  true  pre}
      {post isElem == e elem (*t).m post}

    proc search ( in t : Multiset(T)*, in e : T )
      {pre  true  pre}
      {post true post}

    proc show ( in t : Multiset(T)* )
      {pre  true  pre}
      {post true post}
      
  end


  type SearchTree(T) implements Multiset(T)
  begin
    /* SearchTree variables */
    var l, r := null, null : SearchTree(T)*;
    var info : T;
    var n    : int;
    
    {repinv (l == null \/ (l != null ==> info > (*l).info))
         /\ (r == null \/ (r != null ==> info < (*r).info))
    repinv}
  
    {coupinv true coupinv}
    where {    
      m := {: i : int | 0 <= i /\ i < n | info :} union 
              if (l,r) == (null,null) -> {::}
              [] l == null -> (*r).m 
              [] r == null -> (*l).m
              [] otherwise -> (*l).m union (*r).m 
              fi
    }

//     /* SearchTree procedures and functions */
    func isElem' ( t : SearchTree(T)*, e : T, b : int ) -> boolean
      {pre  true  pre}
      {post true post}
      {bound b bound}
      |[ if t == null      -> false
         [] (*t).info == e -> true
         [] (*t).info  > e -> isElem'((*t).l, e, b-1)
         [] (*t).info  < e -> isElem'((*t).r, e, b-1)
         fi
      ]|

    func isElem ( t : SearchTree(T)*, e : T ) -> boolean
      {pre  true  pre}
      {post true post}
      |[ isElem'(t,e,100) ]|

    proc insert ( inout t : SearchTree(T)*, in e : T )
      {pre  true  pre}
      {post true post}
      ⟦ var P,Q  := t, null : SearchTree(T)*
      ; var i    := 0       : int
      ; var done := false   : boolean
      ; var d               : T
      ; writeln("Hola")
      ; {inv true inv}
        {bound 128 - i bound}
        do i < 128 /\ !done
          -> if P == null -> done := true
             [] otherwise 
              -> d := (*P).info

               ; if e == d -> done := true
                 [] e  < d -> Q,P := P, (*P).l
                 [] e  > d -> Q,P := P, (*P).r
                 [] otherwise -> skip
                 fi
             fi
           ; i := i+1
        od

      ; if P == null 
          -> var node : SearchTree(T)*
           ; new(node)
           ; (*node).info, (*node).n := e, 1
           ; (*node).l   , (*node).r := null, null

           ; if Q == null -> t := node
             [] e < (*Q).info -> (*Q).l := node
             [] e > (*Q).info -> (*Q).r := node
             fi
        [] otherwise -> (*P).n := (*P).n + 1
        fi 
      ⟧
    

    proc auxDelete ( inout t : SearchTree(T)*
                   , inout P : SearchTree(T)*
                   , in    Q : SearchTree(T)*
                   , in    n : int )
      {pre  P != null  pre}
      {post true post}
      {bound 128-n bound}

      |[ if (*P).l == null /\ (*P).r == null
          -> if   Q == null -> t      := null
             [] (*Q).l == P -> (*Q).l := null
             [] (*Q).r == P -> (*Q).r := null
             fi
           ; free(P)
           ; P := null
         [] (*P).l == null 
          -> if   Q == null -> t      := (*P).r
             [] (*Q).l == P -> (*Q).l := (*P).r
             [] (*Q).r == P -> (*Q).r := (*P).r
             fi
           ; free(P)
           ; P := null
         [] (*P).r == null
          -> if   Q == null -> t      := (*P).l
             [] (*Q).l == P -> (*Q).l := (*P).l
             [] (*Q).r == P -> (*Q).r := (*P).l
             fi
           ; free(P)
           ; P := null

         [] otherwise 
          -> var S, R := (*P).r, P : SearchTree(T)*
           ; var i := 0 : int
           ; {inv S != null inv}
             {bound 128-i  bound}
             do i < 128 -> if (*S).l != null -> R,S := S, (*S).l
                           [] otherwise      -> skip
                           fi
                        ; i := i + 1
             od
           ; (*P).info, (*P).n := (*S).info, (*S).n
           ; auxDelete(t,S,R,n+1)
         fi
    
      ]|
    
    proc delete ( inout t : SearchTree(T)*, in e : T )
      {pre true pre}
      {post true post}
      ⟦ var d : T
      ; var P,Q := t, null : SearchTree(T)*
      ; var n,i := 0,0 : int
      ; var done := false : boolean

      ; {inv true inv}
        {bound 128 - i bound}
        do i < 128 /\ !done 
          -> if P == null -> done := true
             [] otherwise 
              -> d := (*P).info
               ; if e == d -> done := true
                 [] e  < d -> Q,P := P, (*P).l
                 [] e  > d -> Q,P := P, (*P).r
                 fi
             fi
            ; i := i+1
        od
      ; if P == null 
         -> write("No quedan ocurrencias por eliminar de ", e)
          ; n := 0
        [] (*P).n  > 1 -> (*P).n, n := (*P).n - 1, (*P).n
        [] (*P).n == 1 -> auxDelete(t,P,Q,0);
        [] otherwise   -> skip
        fi
      ⟧


    proc pad (in p : int)
      {pre  true  pre}
      {post true post}
      |[ var i := 0 : int
       ; {inv true inv}
         {bound p - i bound}
         do i < p -> write(" "); i := i+1 od
      ]|

    proc search ( in t : SearchTree(T)*, in e : T )
      {pre true pre}
      {post true post}
      ⟦ var d : T
      ; var i    := 0     : int
      ; var done := false : boolean
      ; var P    := t     : SearchTree(T)*

      ; {inv true inv}
        {bound 128 - i bound}
        do i < 128  
          -> if P == null -> done := true
             [] otherwise
              -> d := (*P).info
               ; if e == d -> done := true
                 [] e < d  -> P := (*P).l 
                 [] e > d  -> P := (*P).r
                 fi
             fi
           ; i := i + 1
        od

      ; if P == null -> writeln("\n(", e, ",",      0, ")\n")
        [] otherwise -> writeln("\n(", e, ",", (*P).n, ")\n")
        fi
      ⟧

    proc mostrar ( in t : SearchTree(T)*, in p : int)
      {pre  true   pre}
      {post true post}
      {bound 128 - p bound}
      |[ pad(p)
       ; if t == null -> writeln("-")
         [] otherwise -> writeln("(", (*t).info, ",", (*t).n, ")")
                       ; mostrar((*t).l, p + 1)
                       ; mostrar((*t).r, p + 1)
         fi
      ]|

    proc show ( in t : SearchTree(T)* )
      {pre  true pre}
      {post true post}
      ⟦ mostrar(t, 0) ⟧
  end

  

main
  ⟦ var n := 0 : int
  ; var c : char
  
  ; var arr : array[10] of (int*)
  ; var t : SearchTree(char)*
  ; writeln((trace(t == null) \/ c > (*t).info) )
  ; writeln("Debido a que el programa debe terminar,")
  ; writeln("este prompt se emitira solo un maximo de 100 veces")
  ; {inv   true    inv}
    {bound 100-n bound}
    do n < 100 
      -> n := n + 1
       ; writeln("(Intento: ", n, " de 100)")
       ; writeln("Introduzca una letra:")
       ; writeln("\t(T) para Terminar,")
       ; writeln("\t(I) para Insertar un valor en el arbol,")
       ; writeln("\t(E) para Eliminar un valor del arbol,")
       ; writeln("\t(B) para Buscar un valor en el arbol, o")
       ; writeln("\t(M) para Mostrar el arbol en preorden.")
       ; write("> ")
       ; read(c)
       ; if c == 'T' \/ c == 't' -> writeln("Adios :)"); n := 100
         [] c == 'I' \/ c == 'i'
          -> write("Escriba el caracter a insertar: ")
           ; read(c)
           ; insert(t, c)
         [] c == 'E' \/ c == 'e'
          -> write("Escriba el caracter a eliminar: ")
           ; read(c)
           ; delete(t, c)
         [] c == 'B' \/ c == 'b'
          -> write("Escriba el caracter a buscar: ")
           ; read(c)
           ; writeln(isElem(t, c))
         [] c == 'M' \/ c == 'm' -> show(t)
         [] otherwise -> writeln("Has perdido un intento :(")
         fi
    od
  ; free(t)
  ⟧
end